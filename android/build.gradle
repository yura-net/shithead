buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        //classpath 'com.google.gms:google-services:4.4.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'
//apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 34
    buildToolsVersion '34.0.0'
    namespace = "net.yura.shithead"

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = []
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    // this is to avoid the @TargetApi missing on methods giving errors.
    defaultConfig {
        // more info: https://apilevels.com/
        minSdkVersion 14
        // in gradle 8 we HAVE to have this here, as the one in AndroidManifest.xml is ignored
        targetSdkVersion 35
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled true
    }
    lint {
        abortOnError false
    }

    signingConfigs {
        Properties localProperties = new Properties()
        rootProject.file('local.properties').with { if (exists()) localProperties.load(new FileInputStream(it)) }

        SignConfig {
            keyAlias 'yura.net key'
            keyPassword localProperties['keyPassword'] ?: "password"
            storeFile file('key')
            storePassword localProperties['storePassword'] ?: "password"
        }
    }
    if (signingConfigs.SignConfig.storeFile.exists()) {
        logger.lifecycle("USING KEY TO SIGN FROM 'key' FILE!!")
        buildTypes {
            debug {
                signingConfig signingConfigs.SignConfig
            }
            release {
                // this is to allow me to keep android:debuggable="true" in the AndroidManifest.xml
                debuggable true
                signingConfig signingConfigs.SignConfig
            }
        }
    }
}

dependencies {
    implementation project(':client')
    implementation 'org.swingme:swingme-binding-android:1.1.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
}
