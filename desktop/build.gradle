plugins {
    id 'application'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

application {
    mainClassName = 'org.me4se.MIDletRunner'
    applicationName = "shithead"
    // this sets system properties for BOTH running from gradle AND packaging the app with the run script
    applicationDefaultJvmArgs = [ "-Dwidth=320", "-Dheight=560" ]
}

dependencies {
    implementation project(':client')
    implementation ('org.swingme:swingme-binding-desktop:4.1.0') {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation 'com.github.romankh3:image-comparison:4.4.0'
    testImplementation 'org.awaitility:awaitility:4.3.0'
}

jar {
    archiveBaseName.set("shithead-game")
    manifest {
        attributes(
                'Main-Class': project.mainClassName,
                'MIDlet-1': 'Shithead,,net.yura.shithead.client.ShitHeadApplication',
                'versionName': rootProject.ext.versionName,
                'versionCode': rootProject.ext.versionCode,
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

run {
    // this does not run the actual jar, so cant see the info in the manifest
    args 'net.yura.shithead.client.ShitHeadApplication'
    // this line only affects running from gradle
    jvmArgs += [ "-DversionName=$rootProject.ext.versionName", "-DversionCode=$rootProject.ext.versionCode" ]
}

test {
    // Allow access to internal package
    jvmArgs += [ "--add-exports=java.desktop/sun.awt=ALL-UNNAMED" ]
}
